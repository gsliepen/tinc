dnl Process this file with autoconf to produce a configure script.

dnl $Id: configure.in,v 1.13.2.25 2000/11/25 13:33:30 guus Exp $

AC_INIT(src/tincd.c)
AM_INIT_AUTOMAKE(tinc, 1.0pre4-cvs)
AM_CONFIG_HEADER(config.h)

dnl Include the macros from the m4/ directory
AM_ACLOCAL_INCLUDE(m4)

# Enable GNU extensions.
# Define this here, not in acconfig's @TOP@ section, since definitions
# in the latter don't make it into the configure-time tests.
AC_DEFINE([_GNU_SOURCE], [__USE_BSD])

ALL_LINGUAS="es nl"

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_GCC_TRADITIONAL
AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
jm_PERL


AC_ISC_POSIX

dnl Check and set OS

AC_CANONICAL_HOST

case $host_os in
  *linux*)
    AC_DEFINE(HAVE_LINUX)
  ;;
  *freebsd*)
    AC_DEFINE(HAVE_FREEBSD)
  ;;
  *solaris*)
    AC_DEFINE(HAVE_SOLARIS)
  ;;
esac

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([fcntl.h limits.h sys/ioctl.h syslog.h unistd.h \
sys/time.h malloc.h strings.h sys/file.h])

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_FUNC_MEMCMP
AC_FUNC_ALLOCA
AC_TYPE_SIGNAL
AC_CHECK_FUNCS([ftime socket select strtol strerror flock unsetenv \
asprintf putenv strdup fcloseall daemon])
jm_FUNC_MALLOC
jm_FUNC_REALLOC

AM_GNU_GETTEXT


dnl Crypto stuff
tinc_OPENSSL

dnl Support for SunOS

AC_CHECK_FUNC(socket, [], [
  AC_CHECK_LIB(socket, connect)
])
AC_CHECK_FUNC(gethostbyname, [], [
  AC_CHECK_LIB(nsl, gethostbyname)
])


dnl Check for TUN/TAP support in the kernel
tinc_TUNTAP

dnl Check if checkpoint tracing has to be enabled
AC_ARG_ENABLE(tracing,
  [  --enable-tracing        enable checkpoint tracing (debugging only)],
  [ AC_DEFINE(ENABLE_TRACING) ]
)

AC_SUBST(INCLUDES)

AC_OUTPUT([Makefile src/Makefile
doc/Makefile doc/es/Makefile intl/Makefile lib/Makefile
m4/Makefile po/Makefile.in redhat/Makefile debian/Makefile])
